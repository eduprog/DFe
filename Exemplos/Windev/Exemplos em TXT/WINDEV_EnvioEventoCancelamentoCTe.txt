//Criar as configurações mínimas necessárias para consumir o serviço
oConfiguracao is Unimake.Business.DFe.Servicos.Configuracao()
oConfiguracao.TipoDFe				= TipoDFe.CTe
oConfiguracao.CertificadoArquivo	= "C:\Projetos\certificados\UnimakePV.pfx"
oConfiguracao.CertificadoSenha		= "12345678"

//Criar tag do lote de eventos <eventoCTe>
oEventoCTe			is Unimake.Business.DFe.XML.CTe.EventoCTe
oEventoCTe.Versao	= "4.00"

//Criar tag <detEvento>
oDetEventoCanc				is Unimake.Business.DFe.XML.CTe.DetEventoCanc
oDetEventoCanc.VersaoEvento	= "4.00"
oDetEventoCanc.NProt		= "141190000660363"
oDetEventoCanc.XJust		= "Justificativa para cancelamento do CTe de teste"

//Criar tag <infEvento>
oInfEvento is Unimake.Business.DFe.XML.CTe.InfEvento

//Adicionar o Objeto oDetEventoCanc dentro do objeto DetEvento
oInfEvento.DetEvento	= oDetEventoCanc

//Atualizar propriedades da oInfEvento
//IMPORTANTE: Atualização da propriedade TpEvento deve acontecer depois que o DetEvento recebeu o oDetEventoCanc para que funcione sem erro
oInfEvento.COrgao		= 41 // UFBrasil.PR
oInfEvento.ChCTe		= "41191006117473000150550010000579281779843610"
oInfEvento.CNPJ			= "06117473000150"
oInfEvento.DhEvento		= SysDateTime()
oInfEvento.TpEvento		= 110111 //TipoEventoCTe.Cancelamento
oInfEvento.NSeqEvento	= 1
oInfEvento.TpAmb		= 2 //TipoAmbiente.Homologacao   

//Adicionar a tag <infEvento> dentro da tag <eventoCTe>   
oEventoCTe.InfEvento	= oInfEvento  

EDT_RetornoWSString		= "Versao schema: " + CR + oEventoCTe.Versao + CR + CR
EDT_RetornoWSString    += "CNPJ: " + CR + oEventoCTe.InfEvento.CNPJ + CR + CR

WHEN EXCEPTION IN
	//Consumir o serviço
	oRecepcaoEvento is Unimake.Business.DFe.Servicos.CTe.RecepcaoEvento()
	oRecepcaoEvento.SetXMLConfiguracao(oEventoCTe, oConfiguracao)
	
	eventoAssinado is string = oRecepcaoEvento.GetConteudoXMLAssinado()
	
	EDT_RetornoWSString += "XML Assinado: " + CR + eventoAssinado + CR + CR
	
	// Gravar o XML assinado no HD
	fDelete("d:\testenfe\CancelamentoCTe.xml")
	fSaveText("d:\testenfe\CancelamentoCTe.xml", eventoAssinado)
	
	oRecepcaoEvento.Executar(oEventoCTe, oConfiguracao)
	
	//Ver retorno da SEFAZ
	EDT_RetornoWSString += "XML Retornado: " + CR + oRecepcaoEvento.RetornoWSString + CR + CR
	
	//Ver o conteúdo da tag CStat e XMotivo 
	oRetEventoCTe is Unimake.Business.DFe.XML.CTe.RetEventoCTe <-oRecepcaoEvento.Result
	EDT_RetornoWSString += "CStat: " + CR + oRetEventoCTe.InfEvento.CStat + CR + CR
	EDT_RetornoWSString += "XMotivo: " + CR + oRetEventoCTe.InfEvento.XMotivo + CR + CR
	
	SWITCH oRetEventoCTe.InfEvento.CStat
		CASE 134, 135, 156
			oRecepcaoEvento.GravarXmlDistribuicao("d:\testenfe")
			
		OTHER CASE
			//Evento rejeitado
			//Realizar as ações necessárias
	END
DO
	Info(ExceptionInfo(errFullDetails)) //Ele também pega o conteúdo da Exception do lado do C#, importante envolver o código em Try/Catch
END
