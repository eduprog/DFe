//Criar as configurações mínimas necessárias para consumir o serviço
oConfiguracao is Unimake.Business.DFe.Servicos.Configuracao()
oConfiguracao.TipoDFe				= TipoDFe.MDFe
oConfiguracao.TipoEmissao			= TipoEmissao.Normal
oConfiguracao.CertificadoArquivo	= "C:\Projetos\certificados\UnimakePV.pfx"
oConfiguracao.CertificadoSenha		= "12345678"

// Criar o XML do MDFe
oMDFe		is Unimake.Business.DFe.XML.MDFe.MDFe

//Desserializar o XML para criar o objeto a partir de um arquivo no HD
oMDFe <- oMDFe.LoadFromFile("D:\testenfe\41230306117473000150580010000008611017220670-mdfe.xml")

//Desserializar o XML para criar o objeto a partir da string do XML
//xmlMDFe is string=floadtext("D:\testenfe\41240306117473000150580010000008611017220672-mdfe.xml")
//oMDFe <- oMDFe.LoadFromXML(xmlMDFe)

//Recuperar algumas informações do objeto da NFe

EDT_RetornoWSString		= "<chMDFe> " + oMDFe.InfMDFe.Chave + CR 
EDT_RetornoWSString += "<cMDF> " + oMDFe.InfMDFe.Ide.CMDF + CR 
EDT_RetornoWSString += "<nMDF> " + oMDFe.InfMDFe.Ide.NMDF + CR 

WHEN EXCEPTION IN
	//Consumir o serviço
	oAutorizacaoSinc is Unimake.Business.DFe.Servicos.MDFe.AutorizacaoSinc()	
	oAutorizacaoSinc.SetXMLConfiguracao(oMDFe, oConfiguracao)      
	
	//Pode-se gravar o conteudo do XML assinado na base de dados antes do envio, caso queira recuperar para futuro tratamento, isso da garantias
	xmlMDFeAssinado is string = oAutorizacaoSinc.GetConteudoMDFeAssinado()	
	
	// Demonstrar o XML do CTe assinado na tela
	EDT_RetornoWSString += "XML CTe assinado: " + CR + CR + xmlMDFeAssinado + CR + CR
	
	// Gravar o XML assinado no HD antes de enviar
	chaveMDFe is string = oMDFe.InfMDFe.Chave
	fDelete("d:\testenfe\" + chaveMDFe + "-mdfe.xml")
	fSaveText("d:\testenfe\" + chaveMDFe + "-mdfe.xml", xmlMDFeAssinado)
	
	oAutorizacaoSinc.Executar(oMDFe, oConfiguracao)
	
	//Ver a string do XML retornado pela SEFAZ
	EDT_RetornoWSString += "Retorno do webservice: " + CR + CR + oAutorizacaoSinc.RetornoWSString + CR + CR
	
	EDT_RetornoWSString += "<cStat Lote> " + oAutorizacaoSinc.Result.CStat + CR 
	EDT_RetornoWSString += "<xMotivo Lote>" + oAutorizacaoSinc.Result.XMotivo + CR + CR
	
	IF oAutorizacaoSinc.Result.CStat = 104 // 104 = Lote processado com sucesso
		EDT_RetornoWSString += "<cStat MDFe> " + oAutorizacaoSinc.Result.ProtMDFe.InfProt.CStat + CR 
		EDT_RetornoWSString += "<xMotivo MDFe>" + oAutorizacaoSinc.Result.ProtMDFe.InfProt.XMotivo + CR + CR
		
		IF oAutorizacaoSinc.Result.ProtMDFe.InfProt.CStat = 100 // MDFe autorizado
			//Gravar o XML de distribuição do CTe no HD, se preferir assim.
			oAutorizacaoSinc.GravarXmlDistribuicao("d:\testenfe")
			
			//Pegar a string do XML de distribuição para gravar na base de dados, se preferir assim.  
			stringXmlDistribuicao is string = oAutorizacaoSinc.GetMDFeProcResults(oMDFe.InfMDFe.Chave)
			
			EDT_RetornoWSString += "XML de Distribuição: " + CR + CR + stringXmlDistribuicao
		ELSE
			//MDFe rejeitado, fazer devidos tratamentos
		END
	ELSE
		//MDFe rejeitado, fazer devidos tratamentos		
	END
	
	Info("FIM")	
DO
	Info(ExceptionInfo(errFullDetails)) //Ele também pega o conteúdo da Exception do lado do C#, importante envolver o código em Try/Catch
END
