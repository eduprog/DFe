// Estados que disponibilizam a consulta cadastro:
// - Via SVRS..........: AC, ES, RN, PB, SC
// - Webservice próprio: BA, GO, MG, MS, MT, PE, PR, RS, SP
//
// Estados que não disponibilizam a consulta cadastro:
// - AL, AP, CE, DF, MA, PA, PI, RJ, RO, RR, SE, TO
// Para resolver isso utilize a API: https://developers.receitaws.com.br/#/

//Criar as configurações mínimas necessárias para consumir o serviço
oConfiguracao is Unimake.Business.DFe.Servicos.Configuracao()
oConfiguracao.TipoDFe				= TipoDFe.NFe
oConfiguracao.CertificadoArquivo	= "C:\Projetos\certificados\UnimakePV.pfx"
oConfiguracao.CertificadoSenha		= "12345678"

//Criar o XML 
oConsCad is Unimake.Business.DFe.XML.NFe.ConsCad()
oConsCad.Versao	= "2.00"

oInfCons is Unimake.Business.DFe.XML.NFe.InfCons()
oInfCons.CNPJ		= "06117473000150"
oInfCons.UF			= UFBrasil.PR

oConsCad.InfCons	= oInfCons

WHEN EXCEPTION IN
	//Consumir o serviço
	oConsultaCadastro is Unimake.Business.DFe.Servicos.NFe.ConsultaCadastro()	
	oConsultaCadastro.Executar(oConsCad, oConfiguracao)
	
	//Ver o conteúdo da tag CStat e XMotivo 
	oRetConsCad is Unimake.Business.DFe.XML.NFe.RetConsCad <- oConsultaCadastro.Result
	EDT_RetornoWSString = oRetConsCad.InfCons.CStat + " - " + oRetConsCad.InfCons.xMotivo + CR + CR
	EDT_RetornoWSString += "DADOS DO CONTRIBUINTE:" + CR + CR
	EDT_RetornoWSString += "IE: " + oRetConsCad.InfCons.InfCad.IE + CR
	EDT_RetornoWSString += "Razao: " + oRetConsCad.InfCons.InfCad.XNome + CR + CR
	
	//Ver a string completa do XML retornado pelo WEBSERVICE
	EDT_RetornoWSString += "String do XML retornado:" + CR + CR + oConsultaCadastro.RetornoWSString		
DO
	Info(ExceptionInfo(errFullDetails)) //Ele também pega o conteúdo da Exception do lado do C#, importante envolver o código em Try/Catch
END
