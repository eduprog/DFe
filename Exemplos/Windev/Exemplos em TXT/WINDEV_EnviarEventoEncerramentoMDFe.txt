oConfiguracao						is Unimake.Business.DFe.Servicos.Configuracao
oConfiguracao.TipoDfe				= 4 //4=MDFe
oConfiguracao.CertificadoSenha		= "12345678"
oConfiguracao.CertificadoArquivo	= "C:\Projetos\certificados\UnimakePV.pfx"

//Criar tag Evento
oEventoMDFe is Unimake.Business.DFe.XML.MDFe.EventoMDFe
oEventoMDFe.Versao = "3.00"

//Criar tag DetEventoEncMDFe
oDetEventoEncMDFe is Unimake.Business.DFe.XML.MDFe.DetEventoEncMDFe
oDetEventoEncMDFe.VersaoEvento	= "3.00"
oDetEventoEncMDFe.NProt			= "141200000007987"
oDetEventoEncMDFe.CMun			= 3106200
oDetEventoEncMDFe.CUF			= 41
oDetEventoEncMDFe.DtEnc			= SysDateTime()

//Criar tag InfEvento
oInfEvento is Unimake.Business.DFe.XML.MDFe.InfEvento

//Adicionar a tag DetEventoEncMDFe dentro da Tag DetEvento
oInfEvento.DetEvento	= oDetEventoEncMDFe

//Atualizar propriedades da oInfEvento
//IMPORTANTE: Atualização da propriedade TpEvento deve acontecer depois que o DetEvento recebeu o oDetEventoCanc para que funcione sem erro
oInfEvento.COrgao		= 41 // UFBrasil.PR
oInfEvento.ChMDFe		= "41200210859283000185570010000005671227070615"
oInfEvento.CNPJ			= "10859283000185"
oInfEvento.DhEvento		= SysDateTime()
oInfEvento.TpEvento		= 110112 // TipoEventoNFe.Encerramento ###
oInfEvento.NSeqEvento	= 1
oInfEvento.TpAmb		= 2 // TipoAmbiente.Homologacao

//Adicionar a tag InfEvento dentro da tag Evento
oEventoMDFe.InfEvento	= oInfEvento

//Resgatar algumas informações do XML partindo do objeto criado
EDT_RetornoWSString = "<versao>: " + oEventoMDFe.Versao + CR
EDT_RetornoWSString += "<chMDFe>: " + oEventoMDFe.InfEvento.ChMDFe + CR

oDetEvento is Unimake.Business.DFe.XML.MDFe.DetEventoEncMDFe <- oEventoMDFe.InfEvento.DetEvento
EDT_RetornoWSString += "<nProt>: " + oDetEvento.NProt + CR + CR

WHEN EXCEPTION IN
	//Consumir o serviço
	oRecepcaoEvento is Unimake.Business.DFe.Servicos.MDFe.RecepcaoEvento
	oRecepcaoEvento.Executar(oEventoMDFe,  oConfiguracao)
	
	eventoAssinado is string = oRecepcaoEvento.GetConteudoXMLAssinado()
	EDT_RetornoWSString += "XML Assinado: " + CR + eventoAssinado + CR + CR
	
	// Gravar o XML assinado no HD
	fDelete("d:\testenfe\EncerramentoMDFe.xml")
	fSaveText("d:\testenfe\EncerramentoMDFe.xml", eventoAssinado)
	
	//Ver retorno da SEFAZ
	EDT_RetornoWSString += "XML Retornado: " + CR + oRecepcaoEvento.RetornoWSString + CR + CR
	
	//Ver o conteúdo da tag CStat e XMotivo 
	EDT_RetornoWSString += "CStat do Lote Retornado: " + oRecepcaoEvento.Result.InfEvento.CStat + CR
	EDT_RetornoWSString += "XMotivo: " + oRecepcaoEvento.Result.InfEvento.XMotivo + CR + CR
	
	IF oRecepcaoEvento.Result.InfEvento.CStat = 135 // Recebido pelo Sistema de Registro de Eventos, com vinculação do evento no respetivo MDFe
		oRecepcaoEvento.GravarXmlDistribuicao("d:\testenfe") //Grava o XML de distribuição
		
		EDT_RetornoWSString += "String do XML de distribuição do evento:" + CR + CR
		EDT_RetornoWSString += oRecepcaoEvento.ProcEventoMDFeResult.GerarXMLString() + CR + CR
	ELSE					
		//Rejeitado
	END
DO
	Info(ExceptionInfo(errFullDetails)) //Ele também pega o conteúdo da Exception do lado do C#, importante envolver o código em Try/Catch
END
